apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.2"

    defaultConfig {
        //applicationId "net.babys_care.app"
        applicationId "jp.winas.android.foundation"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "logTag", "\"babys-care\""
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    signingConfigs {
        dev {
            storeFile file(devStoreFile)
            storePassword devStorePassword
            keyAlias devKeyAlias
            keyPassword devKeyPassword
        }
        stg {
            storeFile file(stgStoreFile)
            storePassword stgStorePassword
            keyAlias stgKeyAlias
            keyPassword stgKeyPassword
        }
        demo {
            storeFile file(demoStoreFile)
            storePassword demoStorePassword
            keyAlias demoKeyAlias
            keyPassword demoKeyPassword
        }
        prod {
            storeFile file(prodStoreFile)
            storePassword prodStorePassword
            keyAlias prodKeyAlias
            keyPassword prodKeyPassword
        }
    }
    flavorDimensions 'default'
    productFlavors {
        dev {
           // applicationId "net.babys_care.dev"
            applicationId "jp.winas.android.foundation"
            resValue "string", "app_name", "[dev]赤ちゃん成長ナビ"
            signingConfig signingConfigs.dev
        }
        stg {
            applicationId "net.babys_care.stg"
            resValue "string", "app_name", "[stg]赤ちゃん成長ナビ"
            signingConfig signingConfigs.stg
        }
        demo {
            //applicationId "net.babys_care.test"
            applicationId "jp.winas.android.foundation"
            resValue "string", "app_name", "[test]赤ちゃん成長ナビ"
            signingConfig signingConfigs.demo
        }
        prod {
            resValue "string", "app_name", "赤ちゃん成長ナビ"
            signingConfig signingConfigs.prod
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

android.applicationVariants.all { variant ->
    def host = 'dev-babys-care.winas.jp'

    variant.buildConfigField 'String', 'HOST', "\"$host\""
    variant.buildConfigField "String", "BASE_URL", "\"https://$host\""
    variant.buildConfigField "String", "URL_CLINIC_TOP", "\"https://babys-care.net/clinic/\""
    variant.buildConfigField "String", "URL_LOGIN", "\"https://$host/login\""
    variant.buildConfigField "String", "URL_CONTACT_TOP", "\"https://babys-care.net/contact/\""
    variant.buildConfigField "String", "URL_CONTACT_COMP", "\"https://babys-care.net/contact/confirm/thanks/\""
    variant.buildConfigField "String", "URL_AGREEMENT", "\"https://babys-care.net/information/#privacy\""
    variant.buildConfigField "String", "URL_PRIVACY_POLICY", "\"https://babys-care.net/information/#agreement\""
    variant.buildConfigField "String", "URL_IMAGE_DIRECTORY", "\"https://winas-baby-dev.s3-ap-northeast-1.amazonaws.com/\""
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(':foundation')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //Coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutine"

    //Materials
    implementation "com.google.android.material:material:$materila_version"

    //GSON
    implementation "com.google.code.gson:gson:$gson_version"

    // Card View
    implementation "androidx.cardview:cardview:$cardview_version"

    // Recyclerview
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"

    //glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // LifeCycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // OkHttp3
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttp3_version"

    //Firebase
    implementation "com.google.firebase:firebase-messaging:$firebase_messageing"

    //adding paging
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    //FlexBox
    implementation "com.google.android:flexbox:$flex_version"

    //Graph library
    implementation "com.github.PhilJay:MPAndroidChart:$mp_android_chart"

    //Play core
    implementation "com.google.android.play:core-ktx:$play_core_ktx"

    //WorkManager
    implementation "androidx.work:work-runtime-ktx:$work_version"

}
apply plugin: 'com.google.gms.google-services'